plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.5'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'com.github.node-gradle.node' version '2.2.4'
}

group = 'com.buckwheat'
version = '0.0.1'

java {
	sourceCompatibility = '17'
}

jar {
	enabled = false
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

node {
	download = true
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

	// JWT
	implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'
	implementation group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'

	// DB
	implementation group: 'org.hibernate.orm', name: 'hibernate-core'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// ETC
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.apache.commons:commons-lang3'
	
	// redis
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-redis'

	// Querydsl
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	// websocket
	implementation 'org.springframework.boot:spring-boot-starter-websocket'

	// doc
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test:6.1.5'
}

tasks.named('test') {
	useJUnitPlatform()
}

def frontDir = "$projectDir/front-end"

clean {
	delete file('src/main/generated')
}

def profile = project.hasProperty("profile") ? project.property("profile").toString() : "prod"

sourceSets {
	main {
		resources {
			srcDirs = ["$projectDir/src/main/resources", "src/main/resources/resources-${profile}"]
		}
	}
}

processResources {
	dependsOn "copyWebApp"
}

task copyWebApp(type: Copy) {
	from("$frontDir/build") {
		exclude '**/generated/**' // generated 제외
	}

	into "$projectDir/src/main/resources/static"
}
